apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.zj565061763'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 20180524
        versionName "1.1.7"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    api 'com.android.support:appcompat-v7:27.1.1'
    api 'com.android.support:support-v4:27.1.1'
    api 'com.android.support:recyclerview-v7:27.1.1'

    api 'com.github.zj565061763:res:1.0.16'
    api 'com.github.zj565061763:receiver:1.0.5'
    api 'com.github.zj565061763:utils:1.0.7'
    api 'com.github.zj565061763:utils-extend:1.0.2'

    api 'com.fanwe.android:eventbus:1.0.22'
    api 'com.fanwe.android:holder-objects:1.0.5'
    api 'com.fanwe.android:adapter:1.0.9'
    api 'com.fanwe.android:selectmanager:1.0.10'
}

// 指定编码
tasks.withType(Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        failOnError false
    }
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}